#!/usr/bin/env perl

# Copyright (c) 2017 Todd Freed <todd.freed@gmail.com>
# 
# This file is part of autominer.
# 
# autominer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# autominer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License

#
# autominer wrapper for https://github.com/nicehash/sgminer
#

use strict;
use warnings;

use POSIX;

# pick a random port by profile so that multiple instances can coexist
sub get_api_port
{
  my $sum = 0;
  map { $sum += ord } split(//, $ENV{AUTOMINER_PROFILE});

  sprintf("%d", 4000 + ($sum % (1 << 62)))
}

if($ARGV[0] eq "configured")
{
  system("$ENV{AUTOMINER_SGMINER_PATH} -V 1>/dev/null 2>&1");
  print "yes\n" if $? == 0;
}
elsif($ARGV[0] eq "algos")
{
  for my $algo (qw|pascal blake256r8 decred sia|)
  {
    print("$algo\n");
  }
}
elsif($ARGV[0] eq "mine")
{
  my @cmd = (
      "$ENV{AUTOMINER_SGMINER_PATH}"
    , "-T"
    , "-k", "$ENV{AUTOMINER_ALGO}"
    , "-o", "stratum+tcp://$ENV{AUTOMINER_ALGO}.usa.nicehash.com:$ENV{AUTOMINER_PORT}"
    , "-u", "$ENV{AUTOMINER_USERNAME}"
    , "-p", "x"
    , "--intensity", "21"
    , "-w", "64"
    , "-g", "2"
    , "--api-listen"
    , "--api-port", get_api_port()
  );

  if(defined $ENV{AUTOMINER_CARDS_COMMAS})
  {
    push @cmd, "-d", $ENV{AUTOMINER_CARDS_COMMAS};
  }

  # sgminer crashes without this
  my $sgminer_dir = (map { chomp ; $_ } `which $ENV{AUTOMINER_SGMINER_PATH} | xargs realpath | xargs dirname`)[0];
  chdir($sgminer_dir) or die "chdir($sgminer_dir) : $!";

  print(" >> sgminer : @cmd\n");
  exec { $cmd[0] } @cmd;
}
elsif($ARGV[0] eq "perf")
{
  my $cmd = sprintf("echo -n summary | netcat localhost %d", get_api_port());

  while(1)
  {
    sleep 10;

    my $status = (map { chomp ; $_ } `$cmd`)[0];
    $status =~ /MHS 5s=([0-9.]+)/;
    my $rate = $1;
    my $record = sprintf("%10u %s %sH/s\n", time() - $^T, $rate, "M");
    POSIX::write(1, $record, length($record));
  }
}
